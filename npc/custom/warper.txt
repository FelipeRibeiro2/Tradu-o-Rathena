//===== rAthena Script ======================================= 
//= Teletransportadora
//===== Description: ========================================= 
//= A complete - but very condensed - Teletransportadora script.
//===== Additional Comments: =================================
//= 1.0 Initial script By [Euphy].
//= 1.1 Added missing duplicates and fixed coordinates.
//=     Some coordinates written by [Tekno-Kanix] and [ToastOfDoom].
//= 1.2 Added new episodes and simplified functions.
//= 1.3 Added Renewal checks and Instances menu.
//=     Aligned coordinates with @go.
//= 1.4 Added new Guild Dungeons.
//= 1.4a Slight edits.
//= 1.4b Added Wolfchev's Laboratory warp.
//= 1.5 Added Lasagna ,Para Market ,WOE TE ,Instances and settings [sader1992].
//= 1.5a Fix Bifrost Tower
//============================================================

-	script	Teletransportadora	-1,{
function Go; function Disp; function Pick; function Restrict;

// --------------------------------------------------
//	Main Menu:
// --------------------------------------------------

menu	"Última Warp ^777777["+lastwarp$+"]^000000",-,
    	" ~ Cidades",Towns,
    	" ~ Feudos",Fields,
    	" ~ Masmorras",Dungeons,
    	" ~ Castelos da Guilda",Castles,
    	" ~ Masmorras da Guilda",Guild_Dungeons,
    	" ~ Instâncias",Instances,
    	" ~ Áreas Especiais",Special;

	if (lastwarp$ == "")
		message strcharinfo(0),"Você ainda não teleportou para lugar nenhum.";
	else
		warp lastwarp$,lastwarpx,lastwarpy;
	end;

// ------------------- Functions -------------------
// * Go("<map>",<x>,<y>);
//	~ Warps directly to a map.
//
// * Disp("<Menu Option>",<first option>,<last option>);
// * Pick("<map_prefix>"{,<index offset>});
//	~ Dynamic menu and map selection (auto-numbered).
//	~ Fields and Dungeons must use Disp and Pick Functions.
//
// * Disp("<Option 1>:<Option 2>:<etc.>");
// * Pick("","<map1>","<map2>","<etc.>");
//	~ Manual menu and map selection (listed).
//
// * Restrict("<RE | Pre-RE>"{,<menu option numbers>});
//	~ Only allows map for Renewal or Pre-Renewal modes.
//     If menu option numbers are given, only those maps
//     will be restricted (i.e. not for "Go").
//
// Other notes:
//   ~ Array @c[] holds all (x,y) coordinates.
//   ~ Use @c[2] EXCEPT when maps begin dynamically
//	  at 0: use @c[0] and Pick() offset 1.
// --------------------------------------------------

function Go {
	set lastwarp$, getarg(0);
	set lastwarpx, getarg(1,0);
	set lastwarpy, getarg(2,0);
	warp getarg(0),getarg(1,0),getarg(2,0);
	end;
}
function Disp {
	if (getargcount() < 3)
		set @menu$, getarg(0);
	else {
		set @menu$,"";
		for (set .@i,getarg(1); .@i<=getarg(2); set .@i,.@i+1)
			set @menu$, @menu$+getarg(0)+" "+.@i+":";
	}
	return;
}
function Pick {
	set .@warp_block,@warp_block;
	set @warp_block,0;
	if((@f && .OnlyFirstFld) || (@d && .OnlyFirstDun)){
		set .@select,1;
		if(.@warp_block){
			while(.@warp_block & (1<<.@select)){
				.@select += 1;
			}
		}
	}else{
		set .@select, select(@menu$);
	}
	if (getarg(0) == "") {
		set .@i, .@select;
		set .@map$, getarg(.@i);
	} else {
		set .@i, .@select-getarg(1,0);
		set .@map$, getarg(0)+((.@i<10)?"0":"")+.@i;
	}
	if (.@warp_block & (1<<.@select)) {
		message strcharinfo(0),"Este mapa não está habilitado em "+((checkre(0))?"":"Pre-")+"Renewal.";
		end;
	}
	set .@x, @c[.@i*2];
	set .@y, @c[.@i*2+1];
	deletearray @c[0],getarraysize(@c);
	@f = false; @d = false;
	Go(.@map$,.@x,.@y);
}
function Restrict {
	if ((getarg(0) == "RE" && !checkre(0)) || (getarg(0) == "Pre-RE" && checkre(0))) {
		if (getarg(1,0)) {
			set @warp_block,0;
			for (set .@i,1; .@i<getargcount(); set .@i,.@i+1)
				set @warp_block, @warp_block | (1<<getarg(.@i));
		} else {
			message strcharinfo(0),"Este mapa não está habilitado em "+((checkre(0))?"":"Pre-")+"Renewal.";
			end;
		}
	}
	return;
}

// --------------------------------------------------
	Towns:
// --------------------------------------------------
menu	"Prontera",T1, "Alberta",T2, "Aldebaran",T3, "Amatsu",T4, "Ayothaya",T5,
    	"Brasilis",T6, "Comodo",T7, "Dewata",T8, "Eclage",T9, "Einbech",T10, 
    	"Einbroch",T11, "El Dicastes",T12, "Geffen",T13, "Kunlun",T14, "Hugel",T15,
    	"Izlude",T16, "Jawaii",T17, "Lasagna",T18, "Lighthalzen",T19, "Luoyang",T20,
		"Lutie",T21, "Malangdo",T22, "Malaya",T23, "Manuk",T24,
		"Acampamento de expedição midgarts",T25, "Mora",T26, "Morocc",T27, "Moscovia",T28,
		"Ilha Sem Nome",T29, "Niflheim",T30, "Payon",T31, "Rachel",T32, "Esplêndido",T33,
		"Acampamento de thor",T34, "Umbala",T35, "Veins",T36, "Juno",T37;

T1: Go("prontera",155,183);
T2: Go("alberta",28,234);
T3: Go("aldebaran",140,131);
T4: Go("amatsu",198,84);
T5: Go("ayothaya",208,166);
T6: Restrict("RE");
	Go("brasilis",196,217);
T7: Go("comodo",209,143);
T8: Restrict("RE");
	Go("dewata",200,180);
T9: Restrict("RE");
	Go("ecl_in01",48,53);
T10: Go("einbech",63,35);
T11: Go("einbroch",64,200);
T12: Restrict("RE");
	 Go("dicastes01",198,187);
T13: Go("geffen",119,59);
T14: Go("gonryun",160,120);
T15: Go("hugel",96,145);
T16: Go("izlude",128,(checkre(3)?146:114));
T17: Go("jawaii",251,132);
T18: Restrict("RE");
	 Go("lasagna",193,182);
T19: Go("lighthalzen",158,92);
T20: Go("louyang",217,100);
T21: Go("xmas",147,134);
T22: Restrict("RE");
	 Go("malangdo",140,114);
T23: Restrict("RE");
	 Go("malaya",231,200);
T24: Go("manuk",282,138);
T25: Go("mid_camp",210,288);
T26: Restrict("RE");
	 Go("mora",55,146);
T27: Go("morocc",156,93);
T28: Go("moscovia",223,184);
T29: Go("nameless_n",256,215);
T30: Go("niflheim",202,174);
T31: Go("payon",179,100);
T32: Go("rachel",130,110);
T33: Go("splendide",201,147);
T34: Go("thor_camp",246,68);
T35: Go("umbala",97,153);
T36: Go("veins",216,123);
T37: Go("yuno",157,51);

// --------------------------------------------------
	Fields:
// --------------------------------------------------
@f = true;
menu	"Amatsu Feudos",F1, "Ayothaya Feudos",F2, "Bifrost Feudos", F3,
    	"Brasilis Feudos",F4, "Comodo Feudos",F5, "Dewata Feudos",F6,
    	"Eclage Feudos",F7, "Einbroch Feudos",F8, "El Dicastes Feudos",F9,
    	"Geffen Feudos",F10, "Kunlun Feudos",F11, "Hugel Feudos",F12,
		"Lasagna Feudos",F13, "Lighthalzen Feudos",F14, "Luoyang Feudos",F15, 
		"Lutie Feudos",F16, "Malaya Feudos",F17, "Manuk Feudos",F18, 
		"Mjolnir Feudos",F19, "Moscovia Feudos",F20, "Niflheim Feudos",F21, 
		"Florestas de Payon",F22, "Prontera Feudos",F23, "Rachel Feudos",F24, 
		"Deserto de Sograt",F25, "Feudos Esplêndido",F26, "Umbala Feudos",F27, 
		"Veins Feudos",F28, "Juno Feudos",F29;

F1: setarray @c[2],190,197;
	Disp("Amatsu Feudos",1,1); Pick("ama_fild");
F2: setarray @c[2],173,134,212,150;
	Disp("Ayothaya Feudos",1,2); Pick("ayo_fild");
F3: Restrict("RE");
	setarray @c[2],193,220,220,187;
	Disp("Bifrost Feudos",1,2); Pick("bif_fild");
F4: Restrict("RE");
	setarray @c[2],74,32;
	Disp("Brasilis Feudos",1,1); Pick("bra_fild");
F5: Restrict("Pre-RE",5);
	setarray @c[2],180,178,231,160,191,172,228,194,224,203,190,223,234,177,194,175,172,172;
	Disp("Comodo Feudos",1,9); Pick("cmd_fild");
F6: Restrict("RE");
	setarray @c[2],371,212;
	Disp("Dewata Feudos",1,1); Pick("dew_fild");
F7: Restrict("RE");
	setarray @c[2],97,314;
	Disp("Eclage Feudos",1,1); Pick("ecl_fild");
F8: Restrict("Pre-RE",2,10);
	setarray @c[2],142,225,182,141,187,228,185,173,216,173,195,148,272,220,173,214,207,174,196,200;
	Disp("Einbroch Feudos",1,10); Pick("ein_fild");
F9: Restrict("RE");
	setarray @c[2],143,132,143,217;
	Disp("El Dicastes Feudos",1,2); Pick("dic_fild");
F10: Restrict("Pre-RE",13,15);
	 setarray @c[0],46,199,213,204,195,212,257,192,188,171,166,263,248,158,195,191,186,183,221,117,178,218,136,328,240,181,235,235,211,185;
	 Disp("Geffen Feudos",0,14); Pick("gef_fild",1);
F11: setarray @c[2],220,227;
	 Disp("Kunlun Feudos",1,1); Pick("gon_fild");
F12: Restrict("Pre-RE",3,7);
	 setarray @c[2],268,101,222,193,232,185,252,189,196,106,216,220,227,197;
	 Disp("Hugel Feudos",1,7); Pick("hu_fild");
F13: Restrict("RE");
	 setarray @c[2],344,371,20,98;
	 Disp("Lasagna Feudos",1,2); Pick("lasa_fild");	 
F14: setarray @c[2],240,179,185,235,240,226;
	 Disp("Lighthalzen Feudos",1,3); Pick("lhz_fild");
F15: setarray @c[2],229,187;
	 Disp("Luoyang Feudos",1,1); Pick("lou_fild");
F16: setarray @c[2],115,145;
	 Disp("Lutie Feudos",1,1); Pick("xmas_fild");
F17: Restrict("RE");
	 setarray @c[2],40,272,207,180;
	 Disp("Malaya Feudos",1,2); Pick("ma_fild");
F18: setarray @c[2],35,236,35,262,84,365;
	 Disp("Manuk Feudos",1,3); Pick("man_fild");
F19: setarray @c[2],204,120,175,193,208,213,179,180,181,240,195,270,235,202,188,215,205,144,245,223,180,206,196,208;
	 Disp("Mjolnir Feudos",1,12); Pick("mjolnir_");
F20: setarray @c[2],82,104,131,147;
	 Disp("Moscovia Feudos",1,2); Pick("mosk_fild");
F21: setarray @c[2],215,229,167,234;
	 Disp("Niflheim Feudos",1,2); Pick("nif_fild");
F22: Restrict("Pre-RE",5,11);
	 setarray @c[2],158,206,151,219,205,148,186,247,134,204,193,235,200,177,137,189,201,224,160,205,194,150;
	 Disp("Florestas de Payon",1,11); Pick("pay_fild");
F23: setarray @c[0],208,227,190,206,240,206,190,143,307,252,239,213,185,188,193,194,187,218,210,183,195,149,198,164;
	 Disp("Feudos Prontera",0,11); Pick("prt_fild",1);
F24: Restrict("Pre-RE",2,7,9,10,11,13);
	 setarray @c[2],192,162,235,166,202,206,202,208,225,202,202,214,263,196,217,201,87,121,277,181,221,185,175,200,174,197;
	 Disp("Rachel Feudos",1,13); Pick("ra_fild");
F25: if(.Satan_Morocc){
	 setarray @c[2],219,205,177,206,194,182,224,170,198,216,156,187,185,263,206,228,208,238,209,223,85,97,207,202,31,195,38,195;
	 Disp("Sograt Deserto 1:Sograt Deserto 2:Sograt Deserto 3:Sograt Deserto 7:Sograt Deserto 11:Sograt Deserto 12:Sograt Deserto 13:Sograt Deserto 16:Sograt Deserto 17:Sograt Deserto 18:Sograt Deserto 19:Sograt Deserto 20:Sograt Deserto 21:Sograt Deserto 22");
	 Pick("","moc_fild01","moc_fild02","moc_fild03","moc_fild07","moc_fild11","moc_fild12","moc_fild13","moc_fild16","moc_fild17","moc_fild18","moc_fild19","moc_fild20","moc_fild21","moc_fild22");
	 }else{
	 setarray @c[2],219,205,177,206,194,182,146,297,204,197,275,302,224,170,139,123,101,110,341,39,198,216,156,187,185,263,223,222,170,257,206,228,208,238,209,223,85,97;
	 Disp("Sograt Deserto",1,19); Pick("moc_fild");
	 }
F26: setarray @c[2],175,186,236,184,188,204;
	 Disp("Splendide Feudos",1,3); Pick("spl_fild");
F27: setarray @c[2],217,206,223,221,237,215,202,197;
	 Disp("Umbala Feudos",1,4); Pick("um_fild");
F28: Restrict("Pre-RE",5);
	 setarray @c[2],186,175,196,370,222,45,51,250,202,324,150,223,149,307;
	 Disp("Veins Feudos",1,7); Pick("ve_fild");
F29: Restrict("Pre-RE",5,10);
	 setarray @c[2],189,224,192,207,221,157,226,199,223,177,187,232,231,174,196,203,183,214,200,124,195,226,210,304;
	 Disp("Juno Feudos",1,12); Pick("yuno_fild");

// --------------------------------------------------
	Dungeons:
// --------------------------------------------------
@d = true;
menu	"Abismo e lagos",D1, "Masmorras de Amatsu",D2, "Anthell",D3,
    	"Masmorras de Ayothaya",D4, "Masmorras da Praia",D5, "Torre Bifrost",D42,
    	"Laboratórios Biológicos",D6, "Masmorras de Brasilis",D7, "Masmorras de Byalan",D8, "Torre do rélogio",D9,
    	"Minas de carvão",D10, "Bueiros",D11, "Mosteiro Amaldiçoado",D12, "Masmorras de Dewata",D13,
    	"Masmorras de Einbroch",D14, "Gefenia",D15, "Masmorras de Geffen",D16,
    	"Glast Heim",D17, "Masmorras de Kunlun",D18, "Hidden",D19,
    	"Masmorras de Gelo",D20, "Juperos",D21, "Masmorras de Kiel",D22, "Masmorras de Lasagna",D23,
		"Masmorras de Luoyang",D24, "Masmorras de Magma",D25, "Masmorras de Malangdo",D26,
		"Masmorras de Moscovia",D27, "Masmorras de Nidhogg's",D28, "Templo de Odin",D29,
		"Masmorras dos Orcs",D30, "Masmorras de Payon",D31, "Pyramides",D32, "Santuário de Rachel",D33,
    	"Buraco Scaraba",D34, "Esfinge",D35, "Navio afundado",D36, "Torre de Thanatos",D37,
    	"Vulcão de Thor",D38, "Fábrica de Brinquedos",D39, "Masmorras das Tartarugas",D40, "Masmorras de Umbala",D41;

D1: setarray @c[2],261,272,275,270,116,27;
	Disp("Abyss Lakes",1,3); Pick("abyss_");
D2: setarray @c[2],228,11,34,41,119,14;
	Disp("Masmorras de Amatsu",1,3); Pick("ama_dun");
D3: setarray @c[2],35,262,168,170;
	Disp("Anthell",1,2); Pick("anthell");
D4: setarray @c[2],275,19,24,26;
	Disp("Labirinto do santuário antigo:Dentro do antigo santuário"); Pick("ayo_dun");
D5: setarray @c[2],266,67,255,244,23,260;
	Disp("Masmorras da Praia",1,3); Pick("","beach_dun","beach_dun2","beach_dun3");
D6: Restrict("RE",4);
	setarray @c[2],150,288,150,18,140,134,244,52;
	Disp("Bio Lab",1,4); Pick("lhz_dun");
D7: Restrict("RE");
	setarray @c[2],87,47,262,262;
	Disp("Masmorras de Brasilis",1,2); Pick("bra_dun");
D8: Restrict("RE",6);
	setarray @c[0],168,168,253,252,236,204,32,63,26,27,141,187;
	Disp("Masmorras de Byalan",1,6); Pick("iz_dun",1);
D9: setarray @c[2],199,159,148,283,65,147,56,155,297,25,127,169,277,178,268,74;
	Disp("Torre do Relógio 1:Torre do Relógio 2:Torre do Relógio 3:Torre do Relógio 4:Porão 1:Porão 2:Porão 3:Porão 4");
	Pick("","c_tower1","c_tower2","c_tower3","c_tower4","alde_dun01","alde_dun02","alde_dun03","alde_dun04");
D10: setarray @c[2],52,17,381,343,302,262;
	 Disp("Minas de carvão",1,3); Pick("mjo_dun");
D11: setarray @c[2],131,247,19,19,180,169,100,92;
	 Disp("Bueiros",1,4); Pick("","prt_sewb1","prt_sewb2","prt_sewb3","prt_sewb4");
D12: setarray @c[2],51,14,150,11,120,10;
	 Disp("Mosteiro Amaldiçoado",1,3); Pick("abbey");
D13: Restrict("RE");
	 setarray @c[2],285,160,299,29;
	 Disp("Masmorras de Dewata",1,2); Pick("dew_dun");
D14: setarray @c[2],22,14,292,290;
	 Disp("Masmorras de Einbroch",1,2); Pick("ein_dun");
D15: setarray @c[2],40,103,203,34,266,168,130,272;
	 Disp("Gefenia",1,4); Pick("gefenia",0);
D16: setarray @c[0],104,99,115,236,106,132,203,200;
	 Disp("Masmorras de Geffen",1,4); Pick("gef_dun",1);
D17: setarray @c[2],370,304,199,29,104,25,150,15,157,287,147,15,258,255,108,291,171,283,68,277,156,7,12,7,133,271,224,274,14,70,150,14;
	 Disp("Entrance:Castelo 1:Castelo 2:Cavalaria 1:Cavalaria 2:Cemitério:Bueiro 1:Bueiro 2:Bueiro 3:Bueiro 4:St. Mosteiro:Masmorras da Escadaria:Caverna Subterrânea 1:Caverna Subterrânea 2:Caverna Subterrânea 1:Caverna Subterrânea 2");
	 Pick("","glast_01","gl_cas01","gl_cas02","gl_knt01","gl_knt02","gl_chyard","gl_sew01","gl_sew02","gl_sew03","gl_sew04","gl_church","gl_step","gl_dun01","gl_dun02","gl_prison","gl_prison1");
D18: setarray @c[2],153,53,28,113,68,16;
	 Disp("Masmorras de Kunlun",1,3); Pick("gon_dun");
D19: setarray @c[2],176,7,93,20,23,8;
	 Disp("Masmorras Escondidas",1,3); Pick("prt_maze");
D20: setarray @c[2],157,14,151,155,149,22,33,158;
	 Disp("Masmorras de Gelo",1,4); Pick("ice_dun");
D21: setarray @c[2],140,51,53,247,37,63,150,285;
	 Disp("Entrada:Juperos 1:Juperos 2:Essencial");
	 Pick("","jupe_cave","juperos_01","juperos_02","jupe_core");
D22: setarray @c[2],28,226,41,198;
	 Disp("Masmorras de Kiel",1,2); Pick("kh_dun");
D23: Restrict("RE");
	 setarray @c[2],24,143,22,171,190,18;
	 Disp("Masmorras de Lasagna",1,3); Pick("lasa_dun");
D24: setarray @c[2],218,196,282,20,165,38;
	 Disp("A tumba real:Dentro da Tumba Real:Suei Long Gon"); Pick("lou_dun");
D25: setarray @c[2],126,68,47,30;
	 Disp("Masmorras de Magma",1,2); Pick("mag_dun");
D26: Restrict("RE");
	 setarray @c[2],33,230;
	 Disp("Masmorras de Malangdo",1,1); Pick("mal_dun");
D27: setarray @c[2],189,48,165,30,32,135;
	 Disp("Masmorras de Moscovia",1,3); Pick("mosk_dun");
D28: setarray @c[2],61,239,60,271;
	 Disp("Masmorras de Nidhogg's",1,2); Pick("nyd_dun");
D29: setarray @c[2],298,167,224,149,266,280;
	 Disp("Templo Odin",1,3); Pick("odin_tem");
D30: setarray @c[2],32,170,21,185;
	 Disp("Masmorras de Orc",1,2); Pick("orcsdun");
D31: setarray @c[0],21,183,19,33,19,63,155,159,201,204;
	 Disp("Masmorras de Payon",1,5); Pick("pay_dun",1);
D32: Restrict("RE",7,8);
	 setarray @c[2],192,9,10,192,100,92,181,11,94,96,192,8,94,96,192,8;
	 Disp("Pyramides 1:Pyramides 2:Pyramides 3:Pyramides 4:Porão 1:Porão 2:Porão 1 - Modo pesadelo:Porão 2 - Modo pesadelo");
	 Pick("","moc_pryd01","moc_pryd02","moc_pryd03","moc_pryd04","moc_pryd05","moc_pryd06","moc_prydn1","moc_prydn2");
D33: setarray @c[2],140,11,32,21,8,149,204,218,150,9;
	 Disp("Santuário de Rachel",1,5); Pick("ra_san");
D34: Restrict("RE");
	 setarray @c[2],364,44,101,141;
	 Disp("Buraco de Scaraba",1,2); Pick("dic_dun");
D35: setarray @c[2],288,9,149,81,210,54,10,222,100,99;
	 Disp("Esfinge",1,5); Pick("","in_sphinx1","in_sphinx2","in_sphinx3","in_sphinx4","in_sphinx5");
D36: setarray @c[2],69,24,102,27;
	 Disp("Navio afundado",1,2); Pick("treasure");
D37: setarray @c[2],150,39,150,136,220,158,59,143,62,11,89,221,35,166,93,148,29,107,159,138,19,20,130,52;
	 Disp("Torre de Thanatos",1,12); Pick("tha_t");
D38: setarray @c[2],21,228,75,205,34,272;
	 Disp("Vulcão de Thor",1,3); Pick("thor_v");
D39: setarray @c[2],205,15,129,133;
	 Disp("Fábrica de brinquedos",1,2); Pick("xmas_dun");
D40: setarray @c[2],154,49,148,261,132,189,100,192;
	 Disp("Entrada:Masmorra da tartaruga 1:Masmorra da tartaruga  2:Masmorra da tartaruga  3"); Pick("tur_dun");
D41: Restrict("Pre-RE",1,2);
	 setarray @c[2],42,31,48,30,204,78;
	 Disp("Oficina de carpinteiro na árvore:Passagem para um mundo estrangeiro:Fonte de Hvergermil");
	 Pick("","um_dun01","um_dun02","yggdrasil01");
D42: Restrict("RE");
	 setarray @c[2],57,13,64,88,45,14,26,23;
	 Disp("Torre Bifrost",1,4); Pick("ecl_tdun");

// --------------------------------------------------
	Castles:
// --------------------------------------------------
menu	"[FE] Castelos de Aldebaran",C1, "[FE] Castelos de Geffen",C2, "[FE] Castelos de Payon",C3,
    	"[FE] Castelos de Prontera",C4, "[SE] Castelos de Arunafeltz",C5, "[SE] Castelos de Schwarzwald",C6,
		"[TE] Castelos de Aldebaran",C7, "[TE] Castelos de Prontera",C8;

C1: setarray @c[2],48,83,95,249,142,85,239,242,264,90;
	Disp("Neuschwanstein:Hohenschwangau:Nuenberg:Wuerzburg:Rothenburg");
	Pick("","alde_gld","alde_gld","alde_gld","alde_gld","alde_gld");
C2: setarray @c[2],214,75,308,240,143,240,193,278,305,87;
	Disp("Repherion:Eeyolbriggar:Yesnelph:Bergel:Mersetzdeitz");
	Pick("","gef_fild13","gef_fild13","gef_fild13","gef_fild13","gef_fild13");
C3: setarray @c[2],121,233,295,116,317,293,140,160,204,266;
	Disp("Bright Arbor:Scarlet Palace:Holy Shadow:Sacred Altar:Bamboo Grove Hill");
	Pick("","pay_gld","pay_gld","pay_gld","pay_gld","pay_gld");
C4: setarray @c[2],134,65,240,128,153,137,111,240,208,240;
	Disp("Kriemhild:Swanhild:Fadhgridh:Skoegul:Gondul");
	Pick("","prt_gld","prt_gld","prt_gld","prt_gld","prt_gld");
C5: setarray @c[2],158,272,83,47,68,155,299,345,292,107;
	Disp("Mardol:Cyr:Horn:Gefn:Banadis");
	Pick("","aru_gld","aru_gld","aru_gld","aru_gld","aru_gld");
C6: setarray @c[2],293,100,288,252,97,196,137,90,71,315;
	Disp("Himinn:Andlangr:Viblainn:Hljod:Skidbladnir");
	Pick("","sch_gld","sch_gld","sch_gld","sch_gld","sch_gld");
C7: Restrict("RE");
	setarray @c[2],48,83,95,249,142,85,239,242,264,90;
	Disp("Kafragarten 1:Kafragarten 2:Kafragarten 3:Kafragarten 4:Kafragarten 5");
	Pick("","te_alde_gld","te_alde_gld","te_alde_gld","te_alde_gld","te_alde_gld");
C8: Restrict("RE");
	setarray @c[2],134,65,240,128,153,137,111,240,208,240;
	Disp("Glória 1:Glória 2:Glória 3:Glória 4:Glória 5");
	Pick("","te_prt_gld","te_prt_gld","te_prt_gld","te_prt_gld","te_prt_gld");
	
// --------------------------------------------------
	Guild_Dungeons:
// --------------------------------------------------
menu	"Baldur",G1, "Luina",G2, "Valquíria",G3, "Britoniah",G4,
    	"Arunafeltz",G5, "Schwarzwald",G6, "Kafragarten",G7,
		"Glória",G8;

G1: Restrict("RE",2,3);
	setarray @c[2],119,93,119,93,120,130;
	Disp("Baldur F1:Baldur F2:Salão do Abismo");
	Pick("","gld_dun01","gld_dun01_2","gld2_pay");
G2: Restrict("RE",2,3);
	setarray @c[2],39,161,39,161,147,155;
	Disp("Luina F1:Luina F2:Salão do Abismo");
	Pick("","gld_dun02","gld_dun02_2","gld2_ald");
G3: Restrict("RE",2,3);
	setarray @c[2],50,44,50,44,140,132;
	Disp("Valquírias F1:Valquírias F2:Salão do Abismo");
	Pick("","gld_dun03","gld_dun03_2","gld2_prt");
G4: Restrict("RE",2,3);
	setarray @c[2],116,45,116,45,152,118;
	Disp("Britoniah F1:Britoniah F2:Salão do Abismo");
	Pick("","gld_dun04","gld_dun04_2","gld2_gef");
G5: Go("arug_dun01",199,195);
G6: Go("schg_dun01",200,124);
G7: Restrict("RE");
	Go("teg_dun01",42,36);
G8: Restrict("RE");
	Go("teg_dun02",26,160);

// --------------------------------------------------
	Instances:
// --------------------------------------------------
menu	"Bakonawa Lake",I1, "Hospital Bangungot 2F",I2, "Caverna de Buwaya",I3,
		"Torre do Demônio",I4, "Eclage Interior",I5, "Torre sem fim",I6,
		"Ninho de vermes de rosto",I7, "Torneio Mágico Geffen",I8, "Palácio Fantasma",I9,
		"Floresta Nebulosa",I10, "Fábrica de brinquedos de terror",I11, "bueiro Malangdo",I12,
		"Ninho de Nidhoggur",I13, "Caverna do Polvo",I14, "Velho Brilho Heim",I15,
		"Memória de orcs",I16, "Sarah e Fenrir",I17, "Memória de Sara",I18,
		"Santuário Selado",I19, "Laboratório de Wolfchev",I20;

I1: Restrict("RE");
	Go("ma_scene01",172,175);
I2: Restrict("RE");
	Go("ma_dun01",151,8);
I3: Restrict("RE");
	Go("ma_fild02",316,317);
I4: Restrict("RE");
	Go("dali02",137,115);
I5: Restrict("RE");
	Go("ecl_hub01",129,12);
I6: Go("e_tower",72,112);
I7: Restrict("RE");
	Go("dali",85,64);
I8: Restrict("RE");
	Go("dali",94,141);
I9: Restrict("RE");
	Go("dali02",46,128);
I10: Restrict("RE");
	Go("bif_fild01",161,334);
I11: Restrict("RE");
	Go("xmas",234,298);
I12: Restrict("RE");
	Go("mal_in01",164,21);
I13: Go("nyd_dun02",95,193);
I14: Restrict("RE");
	Go("mal_dun01",152,230);
I15: Restrict("RE");
	Go("glast_01",204,268);
I16: Go("gef_fild10",240,198);
I17: Restrict("RE");
	Go("dali02",92,141);
I18: Restrict("RE");
	Go("dali",133,108);
I19: Go("monk_test",306,143);
I20: Restrict("RE");
	Go("lhz_dun04",148,269);

// --------------------------------------------------
	Special:
// --------------------------------------------------
menu	"Salão de Leilões",S1, "Campos de batalha",S2, "Casino",S3, "Fenda Dimensional",S4,
		"Sede do Grupo Eden",S5, "Arena Kunlun",S6, "Izlude Arena",S7, 
		"Arena de corrida de monstros",S8, "Mercado do para",S9, "Turbo Track",S10;

S1: Go("auction_01",22,68);
S2: Go("bat_room",154,150);
S3: Go("cmd_in02",179,129);
S4: Restrict("RE");
	Go("dali",113,82);	
S5: Restrict("RE");
	Go("moc_para01",31,14);
S6: Go("gon_test",48,10);
S7: Go("arena_room",100,88);
S8: Go("p_track01",62,41);
S9: Restrict("RE");
	Go("paramk",97,17);
S10: Go("turbo_room",99,114);

OnInit:
	.Satan_Morocc = true;	// false irá habilitar moc_fild 4,5,6,8,9,10,14,15 enquanto desabilitar moc_fild 20,21,22 O padrão é verdadeiro.
	.OnlyFirstFld = false;	// verdadeiro se teletransportará para o primeiro nível dos Campos. O padrão é falso.
	.OnlyFirstDun = false;	// verdadeiro se teletransportará para o primeiro nível das Masmorras. O padrão é falso.
}

// --------------------------------------------------
//	Duplicates:
// --------------------------------------------------
alb2trea,57,70,6	duplicate(Teletransportadora)	Teletransportadora#tre	10168
alberta,28,240,4	duplicate(Teletransportadora)	Teletransportadora#alb	10168
aldebaran,145,118,4	duplicate(Teletransportadora)	Teletransportadora#alde	10168
amatsu,203,87,4	duplicate(Teletransportadora)	Teletransportadora#ama	10168
ayothaya,209,169,6	duplicate(Teletransportadora)	Teletransportadora#ayo	10168
comodo,194,158,4	duplicate(Teletransportadora)	Teletransportadora#com	10168
einbech,59,38,6	duplicate(Teletransportadora)	Teletransportadora#einbe	10168
einbroch,69,202,4	duplicate(Teletransportadora)	Teletransportadora#einbr	10168
gef_fild10,71,339,4	duplicate(Teletransportadora)	Teletransportadora#orc	10168
geffen,124,72,4	duplicate(Teletransportadora)	Teletransportadora#gef	10168
glast_01,372,308,4	duplicate(Teletransportadora)	Teletransportadora#glh	10168
gonryun,162,122,4	duplicate(Teletransportadora)	Teletransportadora#gon	10168
hugel,101,151,4	duplicate(Teletransportadora)	Teletransportadora#hug	10168
izlu2dun,110,92,4	duplicate(Teletransportadora)	Teletransportadora#izd	10168
izlude,134,150,4	duplicate(Teletransportadora)	Teletransportadora#izl	10168	//Pre-RE: (132,120)
jawaii,253,138,4	duplicate(Teletransportadora)	Teletransportadora#jaw	10168
lighthalzen,162,102,4	duplicate(Teletransportadora)	Teletransportadora#lhz	10168
louyang,208,103,6	duplicate(Teletransportadora)	Teletransportadora#lou	10168
manuk,274,146,6	duplicate(Teletransportadora)	Teletransportadora#man	10168
mid_camp,216,288,4	duplicate(Teletransportadora)	Teletransportadora#mid	10168
mjolnir_02,85,364,4	duplicate(Teletransportadora)	Teletransportadora#mjo	10168
moc_ruins,64,164,4	duplicate(Teletransportadora)	Teletransportadora#moc	10168
morocc,159,97,4	duplicate(Teletransportadora)	Teletransportadora#mor	10168
moscovia,229,191,4	duplicate(Teletransportadora)	Teletransportadora#mos	10168
nameless_n,259,213,4	duplicate(Teletransportadora)	Teletransportadora#nam	10168
niflheim,205,179,4	duplicate(Teletransportadora)	Teletransportadora#nif	10168
pay_arche,42,134,4	duplicate(Teletransportadora)	Teletransportadora#arc	10168
payon,182,108,4	duplicate(Teletransportadora)	Teletransportadora#pay	10168
prontera,158,193,4	duplicate(Teletransportadora)	Teletransportadora#prt	10168
prt_fild05,279,223,6	duplicate(Teletransportadora)	Teletransportadora#cul	10168
rachel,135,116,4	duplicate(Teletransportadora)	Teletransportadora#rac	10168
splendide,205,153,4	duplicate(Teletransportadora)	Teletransportadora#spl	10168
thor_camp,249,76,4	duplicate(Teletransportadora)	Teletransportadora#thor	10168
umbala,106,150,3	duplicate(Teletransportadora)	Teletransportadora#umb	10168
veins,214,123,4	duplicate(Teletransportadora)	Teletransportadora#ve	10168
xmas,150,136,6	duplicate(Teletransportadora)	Teletransportadora#xmas	10168
yuno,162,47,4	duplicate(Teletransportadora)	Teletransportadora#yuno	10168

// --------------------------------------------------
//	Duplicates (Renewal):
// --------------------------------------------------
brasilis,201,222,4	duplicate(Teletransportadora)	Teletransportadora#bra	10168
dewata,204,186,6	duplicate(Teletransportadora)	Teletransportadora#dew	10168
dicastes01,194,194,6	duplicate(Teletransportadora)	Teletransportadora#dic	10168
ecl_in01,51,60,4	duplicate(Teletransportadora)	Teletransportadora#ecl	10168
lasagna,196,187,4	duplicate(Teletransportadora)	Teletransportadora#las	10168
malangdo,134,117,6	duplicate(Teletransportadora)	Teletransportadora#mal	10168
malaya,231,204,4	duplicate(Teletransportadora)	Teletransportadora#ma	10168
mora,57,152,4	duplicate(Teletransportadora)	Teletransportadora#mora	10168
