//===== rAthena Script =======================================
//= Card Removal NPC
//===== By: ==================================================
//= TyrNemesis^
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================

prt_in,28,73,4	script	Velha Sábia#eAcustom	78,{

	set .zenycost,200000;    // custo base dos serviços de remoção de cartão (em Zeny)
	set .percardcost,25000;  // custo por cartão dos serviços de remoção de cartão (em Zeny)
	set .faildestroy,1;      // o removedor de cartão deve ter uma chance de falha que destrói itens? (1 = sim, 0 = não)

	disable_items;
	mes "[Velha Sábia]";
	mes "Bom dia, meu jovem. Eu tenho o poder de remover cartas que você combinou em seu equipamento. Essa ideia te agrada?";
	next;
	switch(select("Sim.:Quanto você cobra?:Não, obrigado.")) {
	case 1:
		mes "[Velha Sábia]";
		mes "Muito bem. Qual item devo examinar para você?";
		next;

		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
			if( getequipisequiped(.@indices[.@i]) )
				set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@menu$, .@menu$ + ":";
		}
		set .@part, .@indices[ select(.@menu$) ];
		if(!getequipisequiped(.@part)) {
			mes "[Velha Sábia]";
			mes "Jovem ... Você não está usando nada lá de que eu possa remover cartas.";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[Velha Sábia]";
			mes "Jovem ... Não há cartas compostas neste item. Não posso fazer nada com isso, estou com medo.";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFSó um minuto!";
			mes "Eu não posso oferecer nenhum dos meus";
			mes "serviços para você porque";
			mes "você está carregando muito";
			mes "material. Coloque seus itens extras em";
			mes "Armazenamento Kafra e venha de novo~";
			close;
		}
		mes "[Velha Sábia]";
		mes "Este item tem " + .@cardcount + " cartões compostos sobre ele. Para realizar minha magia, vou precisar " + (.zenycost+(.@cardcount * .percardcost)) + " zeny, a ^0000FFFragmento Estelar^000000, e uma ^0000FFGema Amarela^000000.";
		next;
		if(select("Muito bem. Faça.:Esquece.") == 2) {
			mes "[Velha Sábia]";
			mes "Muito bem. Retorne imediatamente se você procurar meus serviços.";
			close;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[Velha Sábia]";
			mes "Você não tem todos os itens de que preciso para fazer minha mágica, criança. Volte quando você fizer.";
			close;
		}
		mes "[Velha Sábia]";
		mes "Antes de começar, devo avisá-lo - posso falhar. Se o fizer, posso destruir as cartas, o item ou ambos. Eu não dou reembolso. Dito isso, o que é mais importante para você: os cartões ou o item?";
		next;
		switch(select("Eu mudei de ideia sobre isso.:O item.:As cartas.")) {
		case 1:
			mes "[Velha Sábia]";
			mes "Muito bem. Retorne imediatamente se você procurar meus serviços.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[Velha Sábia]";
		mes "Muito bem. Eu devo começar.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1; //Star_Crumb
		delitem 715,1; //Yellow_Gemstone
		
		// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
		// First value = Total failure chance (item and cards destroyed)
		// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
		// Third value = Harmless failure chance (all that's lost is your investment)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Velha Sábia]";
				mes "O processo foi um fracasso total. Receio que o item e os cartões tenham sido destruídos.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[Velha Sábia]";
					mes "Embora eu tenha conseguido remover os cartões do item, eles foram destruídos no processo. O item, no entanto, está bom.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Velha Sábia]";
					mes "Muito infeliz. Consegui remover os cartões, mas o próprio item foi destruído no processo.";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "[Velha Sábia]";
			mes "Não consegui remover os cartões. Felizmente, no entanto, tanto o item quanto os cartões ainda estão corretos.";
			close;
		}
		next;
		successremovecards .@part;
		mes "[Velha Sábia]";
		mes "The process was a success. Here are your cards and your item. Farewell.";
		close;
	case 2:
		mes "[Velha Sábia]";
		mes "Eu cobro uma taxa fixa de "+callfunc("F_InsertComma",.zenycost)+" zeny, mais "+callfunc("F_InsertComma",.percardcost)+" zeny para cada cartão que removo do item. Além disso, preciso de uma Fragmento Estelar e uma Gema Amarela para fazer minha mágica.";
		close;
	case 3:
		mes "[Velha Sábia]";
		mes "Muito bem. Retorne imediatamente se você procurar meus serviços.";
		close;
	}
}
